Name     Horizontal ;
PartNo   00 ;
Date     11/10/2021 ;
Revision 01 ;
Designer Eric Badger ;
Company  Eric Badger ;
Assembly None ;
Location  ;
Device   g22V10 ;


/*
SVGA timing info

Scanline part	Pixels	Time [microseconds]
Visible area	800	20
Front porch	40	1
Sync pulse	128	3.2
Back porch	88	2.2
Whole line	1056	26.4
*/

/* *************** INPUT PINS *********************/
PIN  1       = PHI2;
PIN [2..11]  = [C11..C2];
PIN [13..14] = [C1..C0];

/* *************** OUTPUT PINS *********************/
PIN   16     = HSYNC;
PIN   17     = HBLANK;
PIN   18     = HDISPLAY;
PIN   22     = NOTC2;
PIN   23     = RESET;

FIELD Counter = [C11..C0];

Display    = Counter:[0000..031F];      /*   0 -  799 */
/*FrontPorch = Counter:[031F..0347];*/  /* 799 -  839 */
SyncPulse1 = Counter:[0347];
SyncPulse2 = Counter:[0348..03C7];      /* 839 -  968 */
/*BackPorch  = Counter:[03C7..041F];*/  /* 968 - 1055 */
Restart    = Counter:[041F];            /* 1055 */


RESET    =  Restart;
HSYNC    = !(SyncPulse1 # SyncPulse2);
HDISPLAY = Display;
HBLANK   = !Display;
NOTC2    = !C2;
